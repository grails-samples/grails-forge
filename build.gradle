buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:views-gradle:1.1.5"
        classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
        classpath 'org.grails.plugins:quartz:2.0.9'
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.1.2'
    }
}

version "1.3.1"
group "appgenerator"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.plugins.views-json"
apply plugin:"com.moowork.node"
apply plugin: 'cloudfoundry'

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-codecs"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-async"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails.plugins:cache"
    compile "org.springframework.boot:spring-boot-cli:1.4.2.RELEASE"
    compile "org.grails.plugins:views-json"
    compile "org.grails.plugins:views-json-templates"
    console "org.grails:grails-console"
    profile "org.grails.profiles:rest-api"
    provided "org.codehaus.groovy:groovy-ant"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testCompile "org.grails:grails-datastore-rest-client"
    testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:2.53.1"
    compile 'org.grails.plugins:quartz:2.0.9'
}

node {
    version = '6.9.5'
    yarnVersion = '0.20.0'
    download = true
    nodeModulesDir = file("src/main/client")
}

task buildClientDev(type: NpmTask, dependsOn: 'yarn') {
    group = 'build'
    description = 'Compile client side assets for development'
    args = ['run', 'buildDev']
}

task buildClient(type: NpmTask, dependsOn: 'yarn') {
    group = 'build'
    description = 'Compile client side assets for production'
    args = ['run', 'build']
}

task buildClientWatch(type: NpmTask, dependsOn: 'yarn') {
    group = 'application'
    description = 'Builds and watches the client side assets for rebuilding'
    args = ['run', 'buildWatch']
}

task clientTest(type: NpmTask, dependsOn: 'yarn') {
    group = 'verification'
    description = 'Executes client side unit tests'
    args = ['run', 'test']
}

task clientClean(type: Delete) {
  doLast {
    delete fileTree(dir: "${projectDir}/src/client/node-modules"), fileTree(dir: "${projectDir}/src/client/dist")
  }
}

task deleteWebappFolder(type: Delete) {
    doLast {
        delete fileTree(dir: "${projectDir}/src/main/webapp"), "${projectDir}/src/main/webapp/images"
    }
}

integrationTest {
    systemProperties System.properties
}

clean.dependsOn clientClean, deleteWebappFolder
war.dependsOn(buildClient)
test.dependsOn(clientTest)

tasks.matching { task ->
    ['test', 'check', 'bootRun', 'integrationTest'].any { it == task.name }
}.each { task ->
    task.dependsOn(buildClientDev)
}

cloudfoundry {
    target = "https://api.run.pivotal.io"
    organization = "guides-grails-org"
    application = "generator"
    space = "generator"
    file = file("build/libs/grails-forge-${version}.war")
    memory = 1024
    instances = 1
    buildpack = "java_buildpack"
}